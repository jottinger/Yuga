
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    // Apply the java-library plugin to add support for Java Library
    id "java"
    id 'java-library'
    id "com.jfrog.bintray" version "1.7.3"
    id "maven-publish"
}

dependencies {
    // Use JUnit test framework
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'junit:junit:4.12'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "glenkalarikkal"
            name "Glen Martin"
            email "glen@messai.in"
        }
    }

    scm {
        url "https://github.com/messai-engineering/Yuga"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar{
                classifier "sources"
            }
            groupId 'in.messai'
            artifactId 'yuga'
            version '1.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Parses text for any date format and returns the date object. No need to know format type.')
                root.appendNode('name', 'yuga')
                root.appendNode('url', 'https://github.com/messai-engineering/Yuga')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    filesSpec {
        from 'build/libs/'
        into '.'
    }

    pkg {
        repo = 'yuga'
        name = 'yuga'
        userOrg = 'messai'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/messai-engineering/Yuga.git'
        version {
            name = '1.0.1'
            desc = '1.0.1'
            released  = new Date()
        }
    }

}

version = '1.0.1'

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.twelfthmile.yuga.Yuga'
        )
    }
}
